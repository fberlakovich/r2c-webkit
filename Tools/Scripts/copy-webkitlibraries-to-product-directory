#!/usr/bin/perl -w

# Copyright (C) 2005, 2008, 2010, 2011, 2012, 2013 Apple Inc. All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
# 1.  Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer. 
# 2.  Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in the
#     documentation and/or other materials provided with the distribution. 
#
# THIS SOFTWARE IS PROVIDED BY APPLE AND ITS CONTRIBUTORS "AS IS" AND ANY
# EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL APPLE OR ITS CONTRIBUTORS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
# THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use FindBin;
use lib $FindBin::Bin;
use webkitdirs;

my $productDir = shift @ARGV;
$productDir = $ENV{BUILT_PRODUCTS_DIR} if !$productDir;

chdirWebKit();

my @librariesToCopy = (
    "libWebKitSystemInterfaceLion.a",
    "libWebKitSystemInterfaceMountainLion.a",
    "libWebCoreSQLite3.a",
);

my $ranlib = `xcrun -find ranlib`;
chomp $ranlib;
foreach my $libName (@librariesToCopy) {
    my $srcLib = "WebKitLibraries/" . $libName;
    my $lib = "$productDir/" . $libName;
    if (!-e $lib || -M $lib > -M $srcLib) {
        print "Updating $lib\n";
        system "ditto", $srcLib, $lib;
        system $ranlib, $lib;
    }
}

(system("mkdir", "-p", "$productDir/usr/local/include") == 0) or die;

sub unpackIfNecessary
{
    my ($targetDir, $sampleFile, $package, $hasLibraries) = @_;
    if (!-e $sampleFile || -M $sampleFile > -M $package) {
        print "Unpacking $package into $targetDir\n";
        (system("tar -C $targetDir -xmf $package") == 0) or die;
        if ($hasLibraries) {
            foreach my $library (`tar -tf $package`) {
                chomp $library;
                print "   Ranlib $library\n";
                system $ranlib, $targetDir . "/" . $library;
            }
        }
    }
}

sub dittoHeaders
{
    my ($srcHeader, $header) = @_;
    if (!-e $header || -M $header > -M $srcHeader) {
        print "Updating $header\n";
        (system("ditto", $srcHeader, $header) == 0) or die;
    }
}

dittoHeaders("WebKitLibraries/WebKitSystemInterface.h", "$productDir/usr/local/include/WebKitSystemInterface.h");
dittoHeaders("WebKitLibraries/WebCoreSQLite3", "$productDir/WebCoreSQLite3");


if ($ENV{ENABLE_FTL_JIT}) {
    # Determine where to get LLVM binaries and headers.
    my $majorDarwinVersion = (split /\./, `uname -r`)[0];
    my $llvmLibraryPackage;
    my $llvmIncludePackage;
    if (defined($ENV{LLVM_LIBRARY_PACKAGE}) && defined($ENV{LLVM_INCLUDE_PACKAGE})) {
        $llvmLibraryPackage = $ENV{LLVM_LIBRARY_PACKAGE};
        $llvmIncludePackage = $ENV{LLVM_INCLUDE_PACKAGE};
    } elsif ($majorDarwinVersion == 11) {
        $llvmLibraryPackage = "WebKitLibraries/LLVMLibrariesLion.tar.bz2";
        $llvmIncludePackage = "WebKitLibraries/LLVMIncludesLion.tar.bz2";
    } elsif ($majorDarwinVersion == 12) {
        $llvmLibraryPackage = "WebKitLibraries/LLVMLibrariesMountainLion.tar.bz2";
        $llvmIncludePackage = "WebKitLibraries/LLVMIncludesMountainLion.tar.bz2";
    } else {
        print "Don't know where to find LLVM!\n";
        print "Try defining LLVM_LIBRARY_PACKAGE and LLVM_INCLUDE_PACKAGE.\n";
        exit 1;
    }

    unpackIfNecessary("$productDir/usr/local/include", "$productDir/usr/local/include/llvm-c/Core.h", $llvmIncludePackage, 0);
    unpackIfNecessary($productDir, "$productDir/libLLVMCore.a", $llvmLibraryPackage, 1);

}
